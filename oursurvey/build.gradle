//querydsl 추가
buildscript {
	ext {
		queryDslVersion = "5.0.0"
		major_version = '0'
		minor_version = '0'
		build_version = '4'
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'

	//querydsl 추가
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"

	id 'java'
}

group = 'com'
version = "$major_version.$minor_version.$build_version"
sourceCompatibility = '17'

configurations {
	all {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}

	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-graphql'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-aop'

	// https://mvnrepository.com/artifact/com.github.ulisesbocchio/jasypt-spring-boot-starter
	implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.5'

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-security
	implementation 'org.springframework.boot:spring-boot-starter-security:2.7.0'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

	//querydsl
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}"

	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-data-redis
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis', version: '2.7.1'

	// https://mvnrepository.com/artifact/org.json/json
	implementation 'org.json:json:20220320'

	// https://mvnrepository.com/artifact/com.querydsl/querydsl-sql
	implementation 'com.querydsl:querydsl-sql:5.0.0'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'org.springframework:spring-webflux'

	implementation "org.springframework.boot:spring-boot-starter-log4j2";
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

	// https://mvnrepository.com/artifact/org.mariadb.jdbc/mariadb-java-client
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

}

tasks.named('test') {
	useJUnitPlatform()
}

// plain.jar 생성방지
jar {
	enabled = false
}

bootJar {
	archivesBaseName = 'oursurvey'
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}
configurations {
	querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}
//querydsl 추가 끝